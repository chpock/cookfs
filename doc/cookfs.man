[manpage_begin cookfs n 1.9.0]
[copyright {2010-2011 Wojciech Kocjan <wojciech@kocjan.org>}]
[copyright {2024 Konstantin Kushnir <chpock@gmail.com>}]
[moddesc   {cookfs}]
[titledesc {cookfs}]
[require Tcl 8.5]
[require cookfs [opt 1.9.0]]

[description]

Package [package cookfs] is a Tcl virtual filesystem (VFS) that allows
storing one or more files in a single file. This is similar to mk4vfs, zipvfs
and many other archive formats.

[para]
The main difference is that cookfs was designed for Tcl-related use and is
optimized for both shipping Tcl scripts as well as delivering large payloads
in an efficient way.

[section {COMMANDS}]

[list_begin definitions]

[call [cmd ::cookfs::Mount] [opt [arg options]] [arg archive] [arg local] [opt [arg options]]]
[call [cmd ::cookfs::Mount] [opt [arg options]] [arg -writetomemory] [arg local] [opt [arg options]]]
[call [cmd ::vfs::cookfs::Mount] [opt [arg options]] [arg archive] [arg local] [opt [arg options]]]

Mounts cookfs archive. Archive is read from [arg archive] and can later be accessed
from Tcl using [arg local] as mount point. Options can preceed or succeed [arg archive]
and [arg local] arguments.

[para]
If [arg -writetomemory] argument is specified, argument [arg archive] may be omitted.
This will create a fully in-memory vfs at the specified mount point.

[para]
The command returns a cookfs handle which is also a command that can be used to perform certain actions on cookfs archive.

[para]
For definition of available options, see [sectref {MOUNT OPTIONS}].

[call [cmd ::cookfs::Unmount] [method fsid]]
[call [cmd ::cookfs::Unmount] [method local]]
Unmounts a cookfs archive.

[para]
The [cmd vfs::unmount] command can also be used to unmount archives,
but it is only available if the tclvfs package was loaded at mount time.
Thus, it is not recommended to unmount archives using the [cmd vfs::unmount] command,
and this feature is only present for backward compatibility.

[para]
For cookfs archives it is very important to properly unmount them after performing all operations as many changes are only stored on unmount operation.

[call [arg cookfsHandle] [method aside] [arg filename]]
Uses a separate file for storing changes to an archive. This can be used to keep changes for a read-only archive in a separate file.

[para]
See [sectref {ASIDE AND WRITE TO MEMORY}] for more details on aside files and how they can be used.

[call [arg cookfsHandle] [method writetomemory]]
Stores all further changes to this archive only in memory.
This can be used to store temporary data or applying changes that do not persist across cookfs filesystem remounts and/or application restarts.

[para]
See [sectref {ASIDE AND WRITE TO MEMORY}] for more details on write to memory feature.

[call [arg cookfsHandle] [method optimizelist] [arg base] [arg filelist]]
Takes a list of files and optimizes them to reduce number of pages read by cookfs.
This is mainly useful when unpacking very large number of files.

[para]
Parameter [arg base] specifies path to be prepended when getting file information.
When list of files is relative to archive root, [arg base] should be empty.
When [arg filelist] contains paths relative to subdirectory of archive root,
[arg base] should specify path to this subdirectory.

[para]
For example if archive contains files
[const contents/dir1/file1] and [const contents/dir1/subdir/file2], the following code can be used:

Specifying [arg filelist] relative to a directory and proper [arg base]:
[example {
% puts [$fsid optimizelist contents/dir1 {file1 subdir/file2}]
subdir/file2
file1
}]

Specifying [arg filelist] relative to root directory:
[example {
% puts [$fsid optimizelist "" {contents/dir1/file1 contents/dir1/subdir/file2}]
contents/dir1/subdir/file2
contents/dir1/file1
}]

[call [arg cookfsHandle] [method getmetadata] [arg parameterName] [opt [arg defaultValue]]]
Gets a parameter from cookfs metadata. If [arg parameterName] is currently set in metadata, value for it is returned.

[para]
If  [arg parameterName] is not currently set, [arg defaultValue] is returned if it was specified.
If [arg defaultValue] was not specified and parameter is not set, an error is thrown.

[para]
See [sectref {COOKFS METADATA}] for more details on metadata storage in cookfs archives.

[call [arg cookfsHandle] [method setmetadata] [arg parameterName] [arg value]]
Set [arg parameterName] to specified [arg value]. If parameter currently exists, it is overwritten.

[para]
See [sectref {COOKFS METADATA}] for more details on metadata storage in cookfs archives.

[call [arg cookfsHandle] [method writeFiles] [opt "[arg filename1] [arg type1] [arg data1] [arg size1] [opt "[arg filename2] [arg type2] [arg data2] [arg size2] [opt [arg ..]]"]"]]
Write one or more files to cookfs archive. Specified as list of one or more 4-element entries describing files.
Each [arg filename] specifies name of the file, relative to archive root.
Elements [arg type] and [arg data] specify source for adding a file as well as actual data.
[arg Size] specifies size of the file. If specified as empty string, it is calculated based on [arg type].

[para]
The following values for [arg type] are accepted:

[list_begin itemized]

[item] [const file] -
[arg data] specifies path to file that should be put in the cookfs archive; path is relative to current working directory, same as for command such as [cmd "file copy"];
file is copied directly and does not require staging file in memory, unlike writing a file in cookfs archive using [cmd "open"]

[item] [const data] -
[arg data] is data to be stored in file as binary

[item] [const channel] -
[arg data] is a valid Tcl channel that should be read by cookfs;
channel is read from current location until end or until [arg size] bytes have been read

[list_end]

[call [arg cookfsHandle] [method filesize]]
Returns size of file up to last stored page.
The size only includes page sizes and does not include overhead for index and additional information used by cookfs.

[call [arg cookfsHandle] [method smallfilebuffersize]]
Returns size of all files that are queued up to be written.

[call [arg cookfsHandle] [method password] [arg secret]]
Specifies the password to be used for encryption. Empty [arg secret] disables
encryption for the following added files.

[para]
If aside changes feature is active for the current VFS, this command will only affect
the corresponding mounted aside archive.

[para]
See [sectref {ENCRYPTION}] for more details on encryption in cookfs.

[list_end]

[section {MOUNT OPTIONS}]

The following options can be specified when mounting a cookfs archive:

[list_begin definitions]

[def "[option -readonly]"]

Mount archive as read-only. Archive must exist and be a valid cookfs archive, otherwise mount will fail.

[def "[option -writetomemory]"]

Enable write to memory feature for this mount.

[para]
See [sectref {ASIDE AND WRITE TO MEMORY}] for more details on write to memory feature.

[def "[option -pagesize] [arg bytes]"]

Specifies maximum size of a page. This specifies how much bytes a chunk
(storing entire file, part of a file or multiple files) can be.
Setting larger values increases memory usage, but may increase compression ratio,
especially for compressors such as bzip2 or LZMA, which compress larger chunks
of information more efficiently.

[para]
See [sectref {COOKFS STORAGE}] for more details on how cookfs stores files, index and metadata.

[def "[option -pagecachesize] [arg numPages]"]

Maximum number of pages to be stored in memory when reading data from cookfs archive.
If 0, no cache is used. Otherwise, up to [arg numPages] are kept in memory for efficient access.
Increasing the value directly affects speed of reading operations, but also increases
memory.

[para]
Maximum number of memory used for cache is number of pages to cache multiplied by
maximum size of a page.

[para]
See [sectref {COOKFS STORAGE}] for more details on how cookfs stores files, index and metadata.

[def "[option -smallfilesize] [arg bytes]"]
Specifies threshold for small files. All files smaller than this value are treated as small files
and are stored and compressed as multiple files for efficiency.

[para]
See [sectref {COOKFS STORAGE}] for more details on how cookfs stores files, index and metadata.

[def "[option -smallfilebuffer] [arg bytes]"]
Specifies maximum buffer for small files. Specifying larger value allows
storing more files in memory before saving them on disk. This can produce
better compression ratio.

[para]
See [sectref {COOKFS STORAGE}] for more details on how cookfs stores files, index and metadata.

[def "[option -volume]"]
Register mount point as Tcl volume - useful for creating mount points in locations that do not exist - such as [arg archive://].

[def "[option -compression] [arg {none|zlib|bz2|lzma|zstd|brotli|custom}][opt :[arg level]]"]

Compression to use for storing new files.

[para]
See [sectref {COMPRESSSION}] for more details on compression in cookfs.

[def "[option -compresscommand] [arg {tcl command}]"]
For [arg custom] compression, specifies command to use for compressing pages.

[para]
See [sectref {COMPRESSSION}] for more details on compression in cookfs.
[def "[option -decompresscommand] [arg {tcl command}]"]

For [arg custom] compression, specifies command to use for decompressing pages.

[para]
See [sectref {COMPRESSSION}] for more details on compression in cookfs.

[def "[option -endoffset] [arg offset]"]
Read archive starting at specified offset, instead of reading from end of file.

[para]
This feature is used when cookfs archive is not stored at end of file - for example if it is
used in tclkit and followed by mk4vfs archive.

[def "[option -setmetadata] [arg list]"]
Set specified metadata after mounting. Convenience for setting metadata in mount command.
List should be a name-value pairs list indicating parameter names and values to set.

[def "[option -noregister]"]
Do not register this mount point with tclvfs. Mainly for internal use.

[def "[option -nocommand]"]
Do not register Tcl command for this cookfs handle. Mainly for internal use.

[def "[option -bootstrap] [arg {tcl code}]"]
Bootstrap code for cookit binaries. Mainly for internal use.

[def "[option -pagehash] [arg hash]"]
Hash function to use for comparing if pages are equal. This is mainly used as pre-check and entire page is still checked for.
Defaults to [const md5], can also be [const crc32]. Mainly for internal/testing at this moment. Do not use.

[def "[option -fsindexobject] [arg fsiagesObject]"]
Do not create cookfs::fsindex object, use specified fsindex object. Mainly for internal use.

[def "[option -pagesobject] [arg pagesObject]"]
Do not create cookfs::pages object, use specified pages object. Mainly for internal use.

[def "[option -shared]"]
Create a multi-threaded cookfs that is available to all application threads.

[para]
See [sectref {MULTI-THREADING SUPPORT}] for more details on multi-threading support in cookfs.

[def "[option -encryptkey]"]
Enables key-based encryption.

[para]
See [sectref {ENCRYPTION}] for more details on encryption in cookfs.

[def "[option -encryptlevel] [arg level]"]
Specifies the encryption level, which must be an unsigned integer from 0 to 31 inclusive.
The default encryption level is 15.

[para]
See [sectref {ENCRYPTION}] for more details on encryption in cookfs.

[def "[option -password] [arg secret]"]
Specifies the password to be used for encryption. The [arg secret] argument must not be an empty string.

[para]
See [sectref {ENCRYPTION}] for more details on encryption in cookfs.

[def "[option -fileset] [arg fileset]"]
For a newly created archive specifies the mode for filesets, which can be [arg auto], [arg platform] or [arg tcl_version]. Or it specifies a certain name for fileset. The mode in this case will be [arg custom].

[para]
When opening an already existing archive, it specifies the fileset that will be activated after opening.

[para]
See [sectref {FILESETS}] for more details on filesets in cookfs.

[list_end]

[section {COOKFS STORAGE}]
Cookfs uses [sectref-external cookfs::pages] for storing all files and
directories in an archive. Information on which pages store what files is stored
in [sectref-external cookfs::fsindex] object, that is stored as part of archive.

[para]
Cookfs stores files in one of the following ways:

[list_begin enumerated]

[enum]
Entire file is stored in a single cookfs page.
This happens if file's size is larger than [arg -smallfilesize],
but smaller than [arg -pagesize].

[enum]
Entire file is stored in multiple cookfs pages.
This happens if file's size is larger than [arg -smallfilesize],
and larger than [arg -pagesize].

[enum]
Multiple files are stored in a single cookfs page.
This happens if file's size is smaller than [arg -smallfilesize].

[list_end]

First and second case are similar and a limit on page size is mainly
used to allow faster seek operations and increate I/O operation speed.

[para]
When compressing large files as a single stream (such as in zipvfs and mk4vfs),
seeking operation requires re-reading and ignoring all data up to new location.
With this approach, seek operations are instant since only new pages are read
and data prior to new seek location is ignored.

[para]
Storing multiple files in a single page is used to increase compression
ratio for multiple small files - such as Tcl scripts. All small files
(whose size does not exceed [arg -smallfilesize]) are grouped by
their extension, followed by their file name. This allows files such as
[const pkgIndex.tcl] to be compressed in same page, which is much more efficient
than compressing each of them independantly.

[section {COMPRESSSION}]
Cookfs uses compression to store pages and filesystem index more efficiently.
Pages are compressed as a whole and independant of files. Filesystem index is
also compressed to reduce file size.

[para]
Compression is specified as [arg -compression] option when mounting an archive.
This option applies to newly stored pages and whenever a file index should be
saved. Existing pages are not re-compressed on compression change.

[para]
The [arg -compression] option accepts a compression method and an optional
compression level, separated by a colon character ([const :]). The compression
level must be an integer in the range of -1 to 255. Different compression
methods define the compression level differently. However, it is safe to specify
a compression level that is lower or higher than what is supported by
a particular method. In these cases, the minimum or maximum level will be used
according to the specific compression method. This means that it is possible
and safe to specify level 255 for any method, and the maximum level
will be used.

[para]
If no compression level is specified, or it is an empty string or -1, then
the default level value specific to the chosen compression method will be used.

[para]
For example:
[example {
# This will mount an archive with the zlib compression method and with default level (6)
cookfs::Mount archive archive -compression zlib

# This will mount an archive with the lzma compression method and with compression level 3
cookfs::Mount archive archive -compression lzma:3

# This will mount an archive with the bzip2 compression method and with the maximum compression level for that method (9)
cookfs::Mount archive archive -compression bz2:255
}]

[para]
Cookfs provides the following compressions:

[list_begin itemized]

[item] [const none] -
no compression is used at all; mainly used for testing purposes or when
content should not be packed at all. The compression level is ignored in this case.

[item] [const zlib] -
use zlib compression to compress - same as with mk4vfs and zipvfs. Compression levels
can be from 1 to 9. The default compression level is 6.

[item] [const bz2] -
use bzip2 for compression; requires specifying [const {--enable-bz2}] when building
cookfs. Compression levels can be from 1 to 9. The default compression level is 9.
The compression level for this method means the block size and is counted as
the level multiplied by 100k bytes. The default compression level of 9 corresponds
to a block size of 900 KB.

[item] [const lzma] -
use lzma for compression; requires specifying [const {--enable-lzma}] when building
cookfs. Compression levels can be from 0 to 9. The default compression level is 5.

[item] [const zstd] -
use Zstandard for compression; requires specifying [const {--enable-zstd}] when building
cookfs. Compression levels can be from 1 to 22. The default compression level is 3.

[item] [const brotli] -
use brotli for compression; requires specifying [const {--enable-brotli}] when building
cookfs. Compression levels can be from 0 to 22. The default compression level is 6.

[item] [const custom] -
use Tcl commands for compressing and decompressing pages. The compression level
is ignored in this case.

[list_end]

For [const custom] compression, [arg -compresscommand] and [arg -decompresscommand]
have to be specified. These commands will be used to compress data and will be called
with data to be compressed or decompressed as additional argument.

[para]
For example the following are sample compress and decompress commands:
[example {
# use vfs::zip to (de)compress data and encode it in HEX
proc testc {d} {
    binary scan [vfs::zip -mode compress $d] H* rc
    return "HEXTEST-$rc"
}
proc testd {d} {
    set rc [vfs::zip -mode decompress [binary format H* [string range $d 8 end]]]
    return $rc
}

cookfs::Mount archive archive -compression custom -compresscommand testc -decompresscommand testd
}]

[para]
See [sectref {COOKFS STORAGE}] for more details on how files are stored in cookfs.

[para]
Default values for [arg -pagesize], [arg -smallfilesize] and
[arg -smallfilebuffer] parameters provided by cookfs have been chosen carefully.
These are outcome of performing multiple tests.
It is not recommended to change it for [const zlib] compression.

[section {COOKFS METADATA}]
Cookfs supports storing metadata in an archive. Metadata are any number of
properties stored in a cookfs archive.

[para]
Cookfs does not impose any limits or rules on naming of properties as well as
possible values, but it is recommended that cookfs internal properties are stored
using [const "cookfs.*"] prefix and cookit related properties use
[const "cookit.*"] format.

[para]
User properties do not need to use any prefix or suffix, but it is recommended
to use form of [const "[arg appname].*"].

[section {ASIDE AND WRITE TO MEMORY}]
Cookfs supports a feature called aside changes. It stores all changes
in a separate file. It can be used for updating files in read-only archives such
as standalone binaries or data on read-only media.

[para]
Aside functionality can be enabled for read-only and read-write archives.
All types of changes can be performed and they will be stored in the new file.
Operations such as deletions from a file are also possible.

[para]
Aside changes are persistent. It is possible to mount an archive, setup an aside
file and perform changes and unmount it. When remounting the archive, changes will
not be visible. However, after enabling aside and pointing to same file as before,
changes will instantly be visible.

[para]
Aside changes do not cause increased use of memory - changes are written to disk in same
fashion, using new file instead of the original one.

[para]
Write to memory feature is slightly different. It allows read-only and read-write
archives to store all changes in memory and those will not be saved back on disk.
This feature is mainly intended for handling temporary files or running legacy code that
requires write access to files inside a read-only archive.

[section {CACHING}]
In order to make I/O operations with pages more efficient and avoid additional
page read attempts, a page cache is used.

[para]
Generally, pages that contain more than one file are more interesting to cache.
For these pages, it is more likely that they will be needed to read
the following files.

[para]
To provide higher priority caching for pages with many files, each cache entry
has a "weight" field. The value of this field is 0 for pages that contain data
from only one file. And the value of this field is 1 if this page contains
data from multiple files. According to this field, the priority for storing
pages in the cache will be implemented. When replacing pages in the cache,
the page with the minimum weight is replaced first.

[para]
However, another issue arises with this approach. There may be a situation
where a page with several files will be stored in the cache, but access to
these files is no longer needed. In this case, the cached page will reside
in the cache and prevent more used pages from being added.

[para]
To solve this issue, each cache entry also has an "age" field. The "age"
field is a special value that is incremented by 1 during page operations.
When this value reaches a certain limit, the page weight is reset to the
default value of 0. Also, if the page is requested from the cache and used,
the value of its "age" will be reset to the original value 0.

[para]
This makes it possible to free up cache space from prioritized but unused pages.

[para]
The concept of "age" has the following nuances:

[para]
It is possible to read a file splitted into a large number of pages. To avoid
adding "age" multiple times for all cache elements when reading each page,
"age" for all items will be incremented only on the very first attempt to
read this file.

[para]
The opposite situation is possible, when a single page contains a large number
of small files. To avoid "age" overflow for other pages when reading these files,
the field "age" will only increase the first time this page is read. If this page
already exists in the cache, then the field "age" for cache items
will not be increased.

[section {MULTI-THREADING SUPPORT}]
By default, cookfs are created that are only available within a single
application thread, but to all interpreters.

[para]
Using the [arg -shared] parameter, you can create a cookfs that will be
available to all application threads.

[para]
The following nuances must be taken into account when working in multi-threaded mode:

[para]
1. cookfs remains bound to the thread and interpreter in which it was created.
This means that cookfs can only be unmounted in its own thread. Also, when
the bound thread and/or interpreter is terminated, the cookfs will be unmounted
and become unavailable to all other threads.

[para]
2. [arg cookfsHandle] and internal [sectref-external cookfs::pages]/[sectref-external cookfs::fsindex]
objects are only available in bound thread. Other threads can only use
standard Tcl commands like [const file], [const open], etc to access and
manipulate files in cookfs.

[para]
3. Tcl callbacks are not available for multi-threaded cookfs.
In particular, it means that compression type [const custom] is not available in this mode.
The following list of options is not available for multi-threaded cookfs:
[list_begin itemized]
[item] [arg -compresscommand]
[item] [arg -decompresscommand]
[item] [arg -asynccompresscommand]
[item] [arg -asyncdecompresscommand]
[list_end]

[para]
4. The multi-thread cookfs is only available when package is built with [const c-vfs] option enabled.

[section {ENCRYPTION}]
Cookfs uses strong AES-256-CBC encryption with PBKDF2-HMAC-SHA256 key derivation.
Encryption strength (resistance to password brute force) can be configured
as described in the [sectref {ENCRYPTION STRENGTH}] section.

[para]
Cookfs supports 2 encryption modes:
[list_begin itemized]
[item] file-based encryption
[item] key-based encryption
[list_end]

[para]
By default, file-based encryption is used. More information about this mode
is available in section [sectref {ENCRYPTION - FILE-BASED}].

[para]
Key-based encryption is quite different from file-based encryption.
More details about this mode can be found in section [sectref {ENCRYPTION - KEY-BASED}].

[para]
The encryption mode is set once when creating an archive and cannot be changed.

[section {ENCRYPTION - FILE-BASED}]
File-based encryption mode is the simplest mode and will be active by default.
In this mode, files are directly encrypted using the specified password.
This means that it is possible to use different passwords for different files.

[para]
To disable encryption it is possible to set an empty password. After that,
new added files will be unencrypted and there will be no access to already
encrypted files in cookfs.

[para]
For example, this use case creates an archive with unprotected Tcl scripts.
These scripts will ask the user for a password and then read other files
using the specified password.

[example {
# This code prepares the archive.
set fsid [cookfs::Mount "archive" "/mount_point"]

# Copy the necessary runtime scripts
file copy ./my_scripts [file join "/mount_point" "scripts"]

# Set the first password password
$fsid password "my-secret-1"

# Add files that will be protected by password "my-secret-1"
file copy ./data1 [file join "/mount_point" "data1"]

# Set another password
$fsid password "my-secret-2"

# Add files that will be protected by password "my-secret-2"
file copy ./data2 [file join "/mount_point" "data2"]

# Unmount and save the archive
cookfs::Unmount "/mount_point"
}]

[para]
This code uses the archive.

[example {
# First mount the archive without a password
set fsid [cookfs::Mount "archive" "/mount_point" -readonly]
# Execute our scripts, which are unencrypted and stored in "scripts" directory
source [file join "/mount_point" "scripts" "main.tcl"]
# Obtain the password or ask the user to provide one
set secret_password [get_password_from_user]
# Set the password as the current password for accessing files in cookfs
$fsid password $secret_password
# Now we can read/use the files in the "data1" or "data2" directory
# if the provided password matches the files in these locations
}]

[para]
If the password is set from the beginning when mounting cookfs, then all
added files will be encrypted. However, it is possible to disable encryption
by setting an empty password and then add files that will not be protected
by encryption.

[para]
To summarize, file-base encryption has these advantages:
[list_begin itemized]
[item] different files may be protected by different passwords
[item] it is possible to open an archive without a password to read unprotected files and set a password later when you need to access protected files
[list_end]
On the other hand, there are the following disadvantages:
[list_begin itemized]
[item] it is impossible to protect information about the file system (file names, sizes, etc.)
[item] it is impossible to change the encryption password for a file, it is only required to recreate the file with a new password
[list_end]

[section {ENCRYPTION - KEY-BASED}]
In this mode, files are encrypted with a random key. In turn, this random key
is encrypted with the specified password and stored in the archive.
When opening the archive, the random key will be decrypted with the specified
password and will be used to decrypt all files in the archive.

[para]
The main advantage of this method is that it is possible to distribute
the same archive but with different passwords for different users/cases.
For this purpose it is not necessary to create a new archive with
a new password each time. It is possible to create a base archive with
a known secret password. When it is necessary to transfer this archive with
the required password, you can simply change the password with which the encryption
key is encrypted. All other data will remain unchanged. For cases where
it is necessary to use the same archive but with different passwords,
this method can save a lot of time in creating an archive for each case,
and also improves reproducibility because all users will have exactly
the same set of files.

[para]
This method has 2 variants when the information about the file system
(file names, sizes, etc.) is encrypted and the archive cannot be opened
without specifying the correct password, and when the information about
the file system is not encrypted.

[para]
To use this mode it is necessary to specify the switch [option -encryptkey]
at the first mounting of cookfs. Information about key-based encryption mode
will be saved in the archive and further opening of the archive will
also use this mode. It is impossible to change the mode in an already
created archive.

[para]
If both options [option -encryptkey] and [option -password] are used when
mounting the archive for the first time, then key-based encryption mode with
encryption of information about the file system (file names, sizes, etc.) will be set.
In this case, it is necessary to specify the password when opening the
file next time.

[para]
If at the first mounting of the archive only switch [option -encryptkey] is used
without option [option -password], then the encryption mode will be set without
encrypting information about the file system. In this case it will be possible
to open the archive without specifying a password and use unencrypted data in it.

[para]
In this mode, the [method password] method works by setting or changing
the password for the protected key.

[para]
For example, this use case creates an archive with unprotected Tcl scripts.
These scripts will ask the user for a password and then read other files
using the specified password.

[example {
# This code prepares the archive.
# First, we will create an archive with -encryptkey and without
# the -password option, which will allow us to further open
# the archive without a password
set fsid [cookfs::Mount "archive" "/mount_point" -encryptkey]

# Copy the necessary runtime scripts
file copy ./my_scripts [file join "/mount_point" "scripts"]

# Set the password password
$fsid password "my-secret-1"

# Add files that will be protected by password "my-secret-1"
file copy ./data1 [file join "/mount_point" "data1"]

# Unmount and save the archive
cookfs::Unmount "/mount_point"
}]

[para]
This code uses the archive:

[example {
# First mount the archive without a password
set fsid [cookfs::Mount "archive" "/mount_point"]

# Execute our scripts, which are unencrypted and stored in "scripts" directory
source [file join "/mount_point" "scripts" "main.tcl"]

# Obtain the password or ask the user to provide one
set secret_password [get_password_from_user]

# Set the password as the current password for accessing files in cookfs
$fsid password $secret_password

# Now we can read/use the files in the "data1" directory
# if the provided password is correct
}]

[para]
This code changes the password for an already prepared archive:

[example {
# First mount the archive
set fsid [cookfs::Mount "archive" "/mount_point" -password "my-secret-1"]

# Set the new password
$fsid password "my-secret-2"

# Unmount and save the archive
cookfs::Unmount "/mount_point"

# After that, to access the files in the "data1" directory from that archive,
# it is necessary to use the password “my-secret-2”.
}]

[para]
This example creates an archive with key-base encryption method and with
all file system information (file names, sizes, etc.) encrypted:

[example {
# This code prepares the archive.
set fsid [cookfs::Mount "archive" "/mount_point" -encryptkey -password "my-secret-1"]
# Add files that will be protected by password "my-secret-1"
file copy ./data1 [file join "/mount_point" "data1"]
# Unmount and save the archive
cookfs::Unmount "/mount_point"
# Now this archive is fully encrypted. To open it, it is necessary to set
# a password when mounting it.
}]

[para]
To summarize, key-base encryption has these advantages:
[list_begin itemized]
[item] it is possible to easily change the password that should be used to open files, it is not necessary to completely recreate the archive
[item] it is possible to open an archive without a password to read unprotected files and set a password later when you need to access protected files
[list_end]
On the other hand, there are the following disadvantages:
[list_begin itemized]
[item] it is impossible to protect information about the file system (file names, sizes, etc.)
[item] it is impossible to have different passwords for different files, all files are encrypted with a single key
[list_end]

[para]
To summarize, key-base encryption with encrypted file system information (file names, sizes, etc.) has these advantages:
[list_begin itemized]
[item] it is possible to easily change the password that should be used to open files, it is not necessary to completely recreate the archive
[item] all data about the contents of the archive is encrypted
[list_end]
On the other hand, there are the following disadvantages:
[list_begin itemized]
[item] it is impossible to open the archive to read unprotected data, the password must be specified when mounting cookfs
[item] it is impossible to have different passwords for different files, all files are encrypted with a single key
[list_end]

[section {ENCRYPTION STRENGTH}]
The encryption strength can be configured as the number of iterations for
PBKDF2-HMAC-SHA256, which can be set by parameter [option -encryptlevel].

[para]
This parameter must have a numeric value from 0 to 31 inclusive. The number
of iterations for values from 0 to 15 are calculated as [option -encryptlevel]*8192.
The number of iterations for values 16 through 31 are calculated
as [option -encryptlevel]*81920. The default value is 15.

[para]
Therefore, it is possible to set the number of iterations in the following range:
[list_begin itemized]
[item] minimum [option -encryptlevel] value 0 - number of iterations 0
[item] default [option -encryptlevel] value 15 - number of iterations 122880
[item] maximum [option -encryptlevel] value 31 - number of iterations 2539520
[list_end]

[para]
Note that the number of iterations affects the delay in setting or changing
the password. The higher the number of iterations, the longer the delay will be.

[para]
The encryption level is set when the archive is created. It is saved in it and
this value will be automatically used for all further opening of the archive.

[section {FILESETS}]
Cookfs allows the use of different sets of files in VFS and switching between them.

[para]
File sets can have either user-defined names or automatic names depending on
the current platform or version of Tcl.

[para]
For example, this allows to create a single archive that when mounted in Linux
and Tcl9.0 environment will have one set of files, and when mounted in Windows
and Tcl8.6 environment will have another set of files.

[para]
The archive can be in 2 modes:
[list_begin itemized]
[item] file sets are disabled and cannot be used (this is the default mode)
[item] file sets are enabled
[list_end]

[para]
To enable the file set feature, it is necessary to use the [option -fileset] option
when initializing the archive. This option accepts either one of the values
[arg auto], [arg platform], [arg tcl_version], or the name of the file set.

[para]
The following modes are enabled:
[list_begin itemized]
[item] [arg auto] value - the active file set is automatically determined by the current Tcl version and platform
[item] [arg platform] value - the active file set is automatically determined by the current platform
[item] [arg tcl_version] value - the active file set is automatically determined by the current Tcl version
[item] if another value is specified - this value will be used for the file set name
[list_end]

[para]
When mounting an archive in which the file set feature is enabled and the mode
is one of [arg auto], [arg platform] or [arg tcl_version], then the active file
set will be selected automatically according to the current Tcl version and/or platform.
If [arg custom] file set mode is set for the archive, then the first available
file set will be selected.

[see_also cookfs_pages cookfs_fsindex]
[keywords vfs cookfs]
[manpage_end]
