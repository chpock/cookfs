tcltest::test cookfsWriter-1.1 "Create empty archive" -constraints runCookfsWriter -setup {
    set file [tcltest::makeFile {} pages.cfs]
} -body {
    cookfs::createArchive $file {} {}
    cookfs::Mount -readonly $file $file
    cookfs::Unmount $file
} -cleanup {
    catch {cookfs::Unmount $file}
    catch {file delete -force $file}
    unset -nocomplain file
} -returnCodes {ok} -match glob -result *

tcltest::test cookfsWriter-1.2 "Create empty archive with bootstrap" -constraints {runCookfsWriter enabledTclCmds} -setup {
    set file [tcltest::makeFile {} pages.cfs]
} -body {
    cookfs::createArchive $file {} {SAMPLE BOOTSTRAP}
    set fsid [vfs::cookfs::Mount -readonly $file $file]
    set p [$fsid getpages]
    if {![string equal [$p get 0] {SAMPLE BOOTSTRAP}]} {
	error "Bootstraps differ"
    }
    vfs::unmount $file
} -cleanup {
    catch {vfs::unmount $file}
    catch {file delete -force $file}
    unset -nocomplain file p fsid
} -returnCodes {ok}

tcltest::test cookfsWriter-1.3 "Create non-empty archive with bootstrap" -constraints {runCookfsWriter enabledTclCmds} -setup {
    set file [tcltest::makeFile {} pages.cfs]
} -body {

    set files {}
    lappend files file1 data {SAMPLECONTENTS} ""
    lappend files cookfswriter.tcl file [file join [tcltest::testsDirectory] .. cookfswriter cookfswriter.tcl] ""

    cookfs::createArchive $file $files {SAMPLE BOOTSTRAP}

    set fsid [vfs::cookfs::Mount -readonly $file $file]

    set p [$fsid getpages]
    if {![string equal [$p get 0] {SAMPLE BOOTSTRAP}]} {
	error "Bootstraps differ"
    }

    set fh [open $file/file1 r]
    fconfigure $fh -translation binary
    if {![string equal [read $fh] {SAMPLECONTENTS}]} {
	error "File file1 does not contain predefined data"
    }
    close $fh

    vfs::unmount $file
} -cleanup {
    catch {close $fh}
    catch {vfs::unmount $file}
    catch {file delete -force $file}
    unset -nocomplain file files fsid p fh
} -returnCodes {ok}
