source [file join [file dirname [info script]] common.tcl]

test cookfsVfsThread-1.1 "Test that shared vfs is available in child thread" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression none -shared]
    set tid [thread::create thread::wait]
    thread::send $tid [list set file $file]
} -body {
    thread::send $tid [list file attribute $file -vfs]
} -cleanup {
    thread::release $tid
    cookfs::Unmount $file
} -result 1

test cookfsVfsThread-1.2 "Test that not shared vfs is unavailable in child thread" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression none]
    set tid [thread::create thread::wait]
    thread::send $tid [list set file $file]
} -body {
    thread::send $tid [list file attribute $file -vfs]
} -cleanup {
    thread::release $tid
    cookfs::Unmount $file
} -match glob -error {bad option "-vfs": must be *}

test cookfsVfsThread-1.3 "Test that shared vfs is available in child thread and unavailable after unmount" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression none -shared]
    set tid [thread::create thread::wait]
    thread::send $tid [list set file $file]
} -body {
    assertEq [thread::send $tid [list file attribute $file -vfs]] 1
    cookfs::Unmount $file
    assertErrMsgMatch [list thread::send $tid [list file attribute $file -vfs]] {bad option "-vfs": must be *}
} -cleanup {
    thread::release $tid
    catch { cookfs::Unmount $file }
} -ok

test cookfsVfsThread-1.4 "Test that shared vfs is available in parent thread and unavailable after unmount" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set tid [thread::create thread::wait]
    thread::send $tid [list set file $file]
    thread::send $tid [list package require cookfs]
} -body {
    thread::send $tid [list cookfs::Mount $file $file -compression none -shared]
    assertEq [file attribute $file -vfs] 1
    thread::send $tid [list cookfs::Unmount $file]
    assertErrMsgMatch [list file attribute $file -vfs] {bad option "-vfs": must be *}
} -cleanup {
    thread::release $tid
    catch { cookfs::Unmount $file }
    catch { ::cookfs::c::reset_cache }
} -ok

test cookfsVfsThread-1.5 "Test that shared vfs is available in parent thread and unavailable after thread released" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set tid [thread::create -joinable thread::wait]
    thread::send $tid [list set file $file]
    thread::send $tid [list package require cookfs]
} -body {
    thread::send $tid [list cookfs::Mount $file $file -compression none -shared]
    assertEq [file attribute $file -vfs] 1
    thread::release $tid
    thread::join $tid
    assertErrMsgMatch [list file attribute $file -vfs] {bad option "-vfs": must be *}
} -cleanup {
    catch { thread::release $tid }
    catch { cookfs::Unmount $file }
    catch { ::cookfs::c::reset_cache }
} -ok

test cookfsVfsThread-1.6 "Test that not shared vfs is unavailable in parent thread" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set tid [thread::create thread::wait]
    thread::send $tid [list set file $file]
    thread::send $tid [list package require cookfs]
} -body {
    thread::send $tid [list cookfs::Mount $file $file -compression none]
    assertErrMsgMatch [list file attribute $file -vfs] {bad option "-vfs": must be *}
    thread::send $tid [list cookfs::Unmount $file]
} -cleanup {
    thread::release $tid
    catch {cookfs::Unmount $file}
} -ok

test cookfsVfsThread-2.1 "Test that -handle is empty in child thread" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression none -shared]
    set tid [thread::create thread::wait]
    thread::send $tid [list set file $file]
} -body {
    thread::send $tid [list file attribute $file -handle]
} -cleanup {
    thread::release $tid
    cookfs::Unmount $file
} -result {}

test cookfsVfsThread-3.1 "Test simple read files from other thread from small buffer" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression none -shared -smallfilesize 128 -smallfilebuffer 1024 -pagesize 1024]
    set file1 [makeBinFile {TEST01} test1 $file]
    set file2 [makeBinFile {TEST02} test2 $file]
    set tid [thread::create thread::wait]
    thread::send $tid [list set file $file]
    thread::send $tid [list set file1 $file1]
    thread::send $tid [list set file2 $file2]
} -body {
    thread::send $tid {
        set result [read [set f [open $file1 rb]]][close $f]
        append result [read [set f [open $file2 rb]]][close $f]
    }
} -cleanup {
    thread::release $tid
    cookfs::Unmount $file
} -result {TEST01TEST02}

test cookfsVfsThread-3.2 "Test simple read files from other thread from pages" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression none -shared -smallfilesize 1 -smallfilebuffer 0 -pagesize 3]
    set file1 [makeBinFile {TEST01X} test1 $file]
    set file2 [makeBinFile {TEST02Y} test2 $file]
    set tid [thread::create thread::wait]
    thread::send $tid [list set file $file]
    thread::send $tid [list set file1 $file1]
    thread::send $tid [list set file2 $file2]
} -body {
    thread::send $tid {
        set result [read [set f [open $file1 rb]]][close $f]
        append result [read [set f [open $file2 rb]]][close $f]
    }
} -cleanup {
    thread::release $tid
    cookfs::Unmount $file
} -result {TEST01XTEST02Y}

test cookfsVfsThread-4.1 "Test error for tcl commands for shared VFS" -constraints {
    threaded enabledCVfs enabledTclCallbacks
} -setup {
    set file [makeFile {} cookfs.cfs]
} -body {
    assertErrMsg { cookfs::Mount $file $file -compression none -shared -compresscommand foo } \
        {cannot use tcl commands in thread-shared mode}
    assertErrMsg { cookfs::Mount $file $file -compression none -shared -decompresscommand foo } \
        {cannot use tcl commands in thread-shared mode}
    assertErrMsg { cookfs::Mount $file $file -compression none -shared -asynccompresscommand foo } \
        {cannot use tcl commands in thread-shared mode}
    assertErrMsg { cookfs::Mount $file $file -compression none -shared -asyncdecompresscommand foo } \
        {cannot use tcl commands in thread-shared mode}
} -cleanup {
    catch { cookfs::Unmount $file }
} -ok

test cookfsVfsThread-5.1 "High load, random reads from multiple threads" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression zlib -shared -smallfilesize 1 -smallfilebuffer 0 -pagesize 2048]
    lassign [randomDatas 3 32768] data1 data2 data3
    # A random data will not be compressed. Add something suitable for compression.
    # Also, this will lead to duplicated pages.
    append data1 [string repeat "TEST" 1024]
    append data2 [string repeat "TEST" 2048]
    set file1 [makeBinFile $data1 test1 $file]
    set file2 [makeBinFile $data2 test2 $file]
    set file3 [makeBinFile $data3 test3 $file]

    set tid1 [thread::create thread::wait]
    thread::send $tid1 [list set rfile $file1]
    thread::send $tid1 [list set rdata $data1]

    set tid2 [thread::create thread::wait]
    thread::send $tid2 [list set rfile $file2]
    thread::send $tid2 [list set rdata $data2]

    set tid3 [thread::create thread::wait]
    thread::send $tid3 [list set rfile $file3]
    thread::send $tid3 [list set rdata $data3]

    foreach tid [list $tid1 $tid2 $tid3] {
        thread::send $tid [list set file $file]
    }
    variable thread_done
} -body {
    foreach tid [list $tid1 $tid2 $tid3] {
        thread::send -async $tid {
            if { [catch {
                for { set i 0 } { $i < 100 } { incr i } {
                    set fp [open $rfile rb]
                    set data [read $fp]
                    close $fp
                    if { $data ne $rdata } {
                        return -code error "\ni: $i\nexpected: $rdata\ngot: $data\n"
                    }
                }
                set ok ok
            } result]} {
                set result "ERROR: $result"
            }
        } thread_done
    }
    # wait 3 threads
    vwait thread_done
    vwait thread_done
    vwait thread_done
    # let's retrieve the results
    foreach tid [list $tid1 $tid2 $tid3] {
        assertEq [thread::send $tid [list set result]] "ok" "bad result from thread $tid"
    }
} -cleanup {
    foreach tid [list $tid1 $tid2 $tid3] {
        thread::release $tid
    }
    cookfs::Unmount $file
} -ok

test cookfsVfsThread-5.2 "High load, random reads from multiple threads (files are in small file buffer)" -constraints {threaded enabledCVfs enabledTclCmds} -setup {
    set file [makeFile {} cookfs.cfs]
    # we need small file buffer 32k*3 + 4*1024 + 4*2048 = 108k
    # Let's make the buffer with 128k bytes
    set fsid [cookfs::Mount $file $file -compression none -shared \
        -smallfilesize 0x20000 -smallfilebuffer 0x20000 -pagesize 0x20000]
    lassign [randomDatas 3 32768] data1 data2 data3
    # A random data will not be compressed. Add something suitable for compression.
    # Also, this will lead to duplicated pages.
    append data1 [string repeat "TEST" 1024]
    append data2 [string repeat "TEST" 2048]
    set file1 [makeBinFile $data1 test1 $file]
    set file2 [makeBinFile $data2 test2 $file]
    set file3 [makeBinFile $data3 test3 $file]

    set tid1 [thread::create thread::wait]
    thread::send $tid1 [list set rfile $file1]
    thread::send $tid1 [list set rdata $data1]

    set tid2 [thread::create thread::wait]
    thread::send $tid2 [list set rfile $file2]
    thread::send $tid2 [list set rdata $data2]

    set tid3 [thread::create thread::wait]
    thread::send $tid3 [list set rfile $file3]
    thread::send $tid3 [list set rdata $data3]

    foreach tid [list $tid1 $tid2 $tid3] {
        thread::send $tid [list set file $file]
    }
    variable thread_done
} -body {
    # Make sure there are no pages
    assertEq [[$fsid getpages] length] 0
    # lock mx on write
    foreach tid [list $tid1 $tid2 $tid3] {
        thread::send -async $tid {
            if { [catch {
                for { set i 0 } { $i < 10 } { incr i } {
                    set fp [open $rfile rb]
                    set data [read $fp]
                    close $fp
                    if { $data ne $rdata } {
                        return -code error "\ni: $i\nexpected: $rdata\ngot: $data\n"
                    }
                }
                set ok ok
            } result]} {
                set result "ERROR: $result"
            }
        } thread_done
    }
    # wait 3 threads
    vwait thread_done
    vwait thread_done
    vwait thread_done
    # let's retrieve the results
    foreach tid [list $tid1 $tid2 $tid3] {
        assertEq [thread::send $tid [list set result]] "ok" "bad result from thread $tid"
    }
} -cleanup {
    foreach tid [list $tid1 $tid2 $tid3] {
        thread::release $tid
    }
    cookfs::Unmount $file
} -ok

test cookfsVfsThread-5.3 "Test a case when 1 thread reads a big pages, another thread reads file on many pages" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression none -shared -smallfilesize 1 -smallfilebuffer 0 -pagesize 16]
    lassign [randomDatas 1 32768] data1
    # one file with 32768 bytes will be split to 16 bytes pages = 2048 pages
    set file1 [makeBinFile $data1 test1 $file]
    # change page size by close/open the vfs
    cookfs::Unmount $file
    set fsid [cookfs::Mount $file $file -compression none -shared -smallfilesize 1 -smallfilebuffer 0 -pagesize 65536]
    lassign [randomDatas 1 163840] data2
    # one file with 163840 bytes will be split to 64k bytes pages = 2.5 pages
    set file2 [makeBinFile $data2 test2 $file]

    set tid1 [thread::create thread::wait]
    thread::send $tid1 [list set rfile $file1]
    thread::send $tid1 [list set rdata $data1]

    set tid2 [thread::create thread::wait]
    thread::send $tid2 [list set rfile $file2]
    thread::send $tid2 [list set rdata $data2]

    foreach tid [list $tid1 $tid2] {
        thread::send $tid [list set file $file]
    }
    variable thread_done
} -body {
    foreach tid [list $tid1 $tid2] {
        thread::send -async $tid {
            if { [catch {
                for { set i 0 } { $i < 10 } { incr i } {
                    set fp [open $rfile rb]
                    set data [read $fp]
                    close $fp
                    if { $data ne $rdata } {
                        return -code error "\ni: $i\nexpected: $rdata\ngot: $data\n"
                    }
                }
                set ok ok
            } result]} {
                set result "ERROR: $result"
            }
        } thread_done
    }
    # wait 2 threads
    vwait thread_done
    vwait thread_done
    # let's retrieve the results
    foreach tid [list $tid1 $tid2] {
        assertEq [thread::send $tid [list set result]] "ok" "bad result from thread $tid"
    }
} -cleanup {
    foreach tid [list $tid1 $tid2] {
        catch { thread::release $tid }
    }
    cookfs::Unmount $file
} -ok

test cookfsVfsThread-6.1 "High load, write files from multiple threads" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression zlib -shared -smallfilesize 1 -smallfilebuffer 0 -pagesize 2048]
    lassign [randomDatas 3 32768] data1 data2 data3
    # A random data will not be compressed. Add something suitable for compression.
    # Also, this will lead to duplicated pages.
    append data1 [string repeat "TEST" 1024]
    append data2 [string repeat "TEST" 2048]
    set file1 [file join $file test1]
    set file2 [file join $file test2]
    set file3 [file join $file test3]

    set tid1 [thread::create thread::wait]
    thread::send $tid1 [list set rfile $file1]
    thread::send $tid1 [list set rdata $data1]

    set tid2 [thread::create thread::wait]
    thread::send $tid2 [list set rfile $file2]
    thread::send $tid2 [list set rdata $data2]

    set tid3 [thread::create thread::wait]
    thread::send $tid3 [list set rfile $file3]
    thread::send $tid3 [list set rdata $data3]

    foreach tid [list $tid1 $tid2 $tid3] {
        thread::send $tid [list set file $file]
    }
    variable thread_done
    variable filex
    variable datax
    variable i
} -body {
    foreach tid [list $tid1 $tid2 $tid3] {
        thread::send -async $tid {
            if { [catch {
                for { set i 0 } { $i < 10 } { incr i } {
                    set fp [open "$rfile.$i" wb]
                    puts -nonewline $fp "$i$rdata"
                    close $fp
                }
                set ok ok
            } result]} {
                set result "ERROR: $result"
            }
        } thread_done
    }
    # wait 3 threads
    vwait thread_done
    vwait thread_done
    vwait thread_done
    # let's retrieve the results
    foreach tid [list $tid1 $tid2 $tid3] {
        assertEq [thread::send $tid [list set result]] "ok" "bad result from thread $tid"
    }
    foreach { filex datax } [list $file1 $data1 $file2 $data2 $file3 $data3] {
        for { set i 0 } { $i < 10 } { incr i } {
            assertEq [viewBinFile "$filex.$i"] "$i$datax" "[file tail $filex].$i - doesn't match"
        }
    }
} -cleanup {
    foreach tid [list $tid1 $tid2 $tid3] {
        catch { thread::release $tid }
    }
    cookfs::Unmount $file
} -ok

if { [::tcl::pkgconfig get threaded] } {
    # Initialize tsv before first use to avoid memory leak messages for tsv
    # structures initialized during test execution
    tsv::set thread_ready 1 1
    tsv::unset thread_ready
}

test cookfsVfsThread-7.1 "VFS terminated while other thread reads a big file" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression none -shared -smallfilesize 0x100000 -smallfilebuffer 0 -pagesize 0x100000]
    set data1 [string repeat "TEST" 0x100000]
    set file1 [makeBinFile $data1 test1 $file]
    set tid [thread::create thread::wait]
    thread::send $tid [list set file $file]
    thread::send $tid [list set file $file]
    thread::send $tid [list set rfile $file1]
    thread::send $tid [list set rdata $data1]
    variable thread_done
} -body {
    thread::send -async $tid {
        set fp [open $rfile rb]
        set count 0
        tsv::set thread_ready 1 1
        catch {
            while { ![tsv::exists thread_ready 2] || [incr count_ready] < 10 } {
                set data [read $fp]
                seek $fp 0
                incr count
            }
            set ok "notok: $count cycles"
        } result
    } thread_done
    # wait until thread opens the file
    while { ![tsv::exists thread_ready 1] } { after 10 }
    # Unmount cookfs
    cookfs::Unmount $file
    tsv::set thread_ready 2 1
    # wait 1 threads
    vwait thread_done
    # let's retrieve the results
    # We accept multiple results. The exact result depends on the state
    # of the thread when the VFS was unmounted
    assertMatchAny [thread::send $tid [list set result]] {
        {error during seek on "*": no such device}
        {error reading "*": I/O error}
    }
} -cleanup {
    tsv::unset thread_ready
    thread::release $tid
    catch { cookfs::Unmount $file }
} -ok

test cookfsVfsThread-7.2 "VFS terminated while other thread writes a big file" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression none -shared -smallfilesize 0x100000 -smallfilebuffer 0 -pagesize 0x100000]
    set data1 [string repeat "TEST" 0x100000]
    set file1 [file join $file test1]
    set tid [thread::create thread::wait]
    thread::send $tid [list set file $file]
    thread::send $tid [list set rfile $file1]
    thread::send $tid [list set rdata $data1]
    variable thread_done
} -body {
    thread::send -async $tid {
        set fp [open $rfile wb]
        set count 0
        tsv::set thread_ready 1 1
        catch {
            while { ![tsv::exists thread_ready 2] || [incr count_ready] < 10 } {
                puts -nonewline $fp $rdata
                close $fp
                set fp [open $rfile wb]
            }
            set ok "notok: $count cycles"
        } result
    } thread_done
    # wait until thread opens the file
    while { ![tsv::exists thread_ready 1] } { after 10 }
    # Unmount cookfs
    cookfs::Unmount $file
    tsv::set thread_ready 2 1
    # wait 1 threads
    vwait thread_done
    # let's retrieve the results
    # We accept multiple results. The exact result depends on the state
    # of the thread when the VFS was unmounted
    assertMatchAny [thread::send $tid [list set result]] {
        {stalled * object detected}
        {couldn't open "*": not a directory}
        {couldn't open "*": no such file or directory}
    }
} -cleanup {
    tsv::unset thread_ready
    thread::release $tid
    catch { cookfs::Unmount $file }
} -ok

test cookfsVfsThread-7.3 "VFS terminated while other thread opens a file for reading (from small file buffer)" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression none -shared -smallfilesize 0x100000 -smallfilebuffer 0x100000 -pagesize 0x100000]
    set data1 "test"
    set file1 [makeBinFile $data1 test1 $file]
    set tid [thread::create thread::wait]
    thread::send $tid [list set file $file]
    thread::send $tid [list set rfile $file1]
    thread::send $tid [list set rdata $data1]
    variable thread_done
} -body {
    thread::send -async $tid {
        # If this test works as expected, then this thread will be able to open
        # the file multiple times before the VFS is unmounted. So, here we also
        # count hoy many times the file was successfully opened.
        set count 0
        catch {
            while { ![tsv::exists thread_ready 2] || [incr count_ready] < 10 } {
                set fp [open $rfile rb]
                if { !$count } {
                    tsv::set thread_ready 1 1
                }
                incr count
                close $fp
            }
            set ok "notok: $count cycles"
        } result
        # Add a 0 or 1 flag to the result whether the file has been opened
        # at least once.
        set result [list [expr { $count > 0 }] $result]
    } thread_done
    # wait until thread opens the file
    while { ![tsv::exists thread_ready 1] } { after 10 }
    # Unmount cookfs
    cookfs::Unmount $file
    tsv::set thread_ready 2 1
    # wait 1 threads
    vwait thread_done
    # let's retrieve the results
    # We accept multiple results. The exact result depends on the state
    # of the thread when the VFS was unmounted
    assertMatchAny [thread::send $tid [list set result]] {
        {1 {couldn't open "*": stalled writer object detected}}
        {1 {couldn't open "*": bad file number}}
        {1 {couldn't open "*": no locks available}}
        {1 {couldn't open "*": not a directory}}
        {1 {couldn't open "*": no such file or directory}}
        {1 {couldn't open "*": no such device}}
        {1 {couldn't open "*": stalled fsindex object detected}}
    }
} -cleanup {
    tsv::unset thread_ready
    thread::release $tid
    catch { cookfs::Unmount $file }
} -ok

test cookfsVfsThread-7.4 "VFS terminated while other thread opens a file for reading (from pages)" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression none -shared -smallfilesize 0x100000 -smallfilebuffer 0 -pagesize 0x100000]
    set data1 "test"
    set file1 [makeBinFile $data1 test1 $file]
    set tid [thread::create thread::wait]
    thread::send $tid [list set file $file]
    thread::send $tid [list set rfile $file1]
    thread::send $tid [list set rdata $data1]
    variable thread_done
} -body {
    thread::send -async $tid {
        # If this test works as expected, then this thread will be able to open
        # the file multiple times before the VFS is unmounted. So, here we also
        # count hoy many times the file was successfully opened.
        set count 0
        catch {
            while { ![tsv::exists thread_ready 2] || [incr count_ready] < 10 } {
                set fp [open $rfile rb]
                if { !$count } {
                    tsv::set thread_ready 1 1
                }
                incr count
                close $fp
            }
            set ok "notok: $count cycles"
        } result
        # Add a 0 or 1 flag to the result whether the file has been opened
        # at least once.
        set result [list [expr { $count > 0 }] $result]
    } thread_done
    # wait until thread opens the file
    while { ![tsv::exists thread_ready 1] } { after 10 }
    # Unmount cookfs
    cookfs::Unmount $file
    tsv::set thread_ready 2 1
    # wait 1 threads
    vwait thread_done
    # let's retrieve the results
    assertMatchAny [thread::send $tid [list set result]] {
        {1 {couldn't open "*": bad file number}}
        {1 {couldn't open "*": no locks available}}
        {1 {couldn't open "*": not a directory}}
        {1 {couldn't open "*": no such file or directory}}
        {1 {could not read "*": no such device}}
        {1 {couldn't open "*": no such device}}
        {1 {couldn't open "*": stalled fsindex object detected}}
    }
} -cleanup {
    tsv::unset thread_ready
    thread::release $tid
    catch { cookfs::Unmount $file }
} -ok

test cookfsVfsThread-7.5 "VFS terminated while other thread opens a file for writing" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression none -shared -smallfilesize 0x100000 -smallfilebuffer 0 -pagesize 0x100000]
    set data1 "test"
    set file1 [makeBinFile $data1 test1 $file]
    set tid [thread::create thread::wait]
    thread::send $tid [list set file $file]
    thread::send $tid [list set rfile $file1]
    thread::send $tid [list set rdata $data1]
    variable thread_done
} -body {
    thread::send -async $tid {
        # If this test works as expected, then this thread will be able to open
        # the file multiple times before the VFS is unmounted. So, here we also
        # count hoy many times the file was successfully opened.
        set count 0
        catch {
            while { ![tsv::exists thread_ready 2] || [incr count_ready] < 10 } {
                set fp [open "$rfile.$count" wb]
                if { !$count } {
                    tsv::set thread_ready 1 1
                }
                incr count
                close $fp
            }
            set ok "notok: $count cycles"
        } result
        # Add a 0 or 1 flag to the result whether the file has been opened
        # at least once.
        set result [list [expr { $count > 0 }] $result]
    } thread_done
    # wait until thread opens the file
    while { ![tsv::exists thread_ready 1] } { after 10 }
    # Unmount cookfs
    cookfs::Unmount $file
    tsv::set thread_ready 2 1
    # wait 1 threads
    vwait thread_done
    # let's retrieve the results
    # We accept multiple results. The exact result depends on the state
    # of the thread when the VFS was unmounted
    assertMatchAny [thread::send $tid [list set result]] {
        {1 {couldn't open "*": no locks available}}
        {1 {couldn't open "*": not a directory}}
        {1 {couldn't open "*": no such file or directory}}
        {1 {couldn't open "*": no such device}}
        {1 {stalled * object detected}}
        {1 {couldn't open "*": stalled fsindex object detected}}
        {1 {error during seek on "*": no such device}}
    }
} -cleanup {
    tsv::unset thread_ready
    thread::release $tid
    catch { cookfs::Unmount $file }
} -ok

test cookfsVfsThread-7.6 "VFS terminated while other thread accesses file stats" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression none -shared -smallfilesize 0x100000 -smallfilebuffer 0 -pagesize 0x100000]
    set data1 "test"
    set file1 [makeBinFile $data1 test1 $file]
    set tid [thread::create thread::wait]
    thread::send $tid [list set file $file]
    thread::send $tid [list set rfile $file1]
    thread::send $tid [list set rdata $data1]
    variable thread_done
} -body {
    thread::send -async $tid {
        # If this test works as expected, then this thread will be able to stat
        # the file multiple times before the VFS is unmounted. So, here we also
        # count how many times stat was read successfully
        set count 0
        catch {
            while { ![tsv::exists thread_ready 2] || [incr count_ready] < 10 } {
                file stat $rfile x
                if { !$count } {
                    tsv::set thread_ready 1 1
                }
                incr count
            }
            set ok "notok: $count cycles"
        } result
        # Add a 0 or 1 flag to the result whether file stat was read
        # at least once.
        set result [list [expr { $count > 0 }] $result]
    } thread_done
    # wait until thread opens the file
    while { ![tsv::exists thread_ready 1] } { after 10 }
    # Unmount cookfs
    cookfs::Unmount $file
    tsv::set thread_ready 2 1
    # wait 1 threads
    vwait thread_done
    # let's retrieve the results
    # We accept multiple results. The exact result depends on the state
    # of the thread when the VFS was unmounted
    assertMatchAny [thread::send $tid [list set result]] {
        {1 {couldn't open "*": bad file number}}
        {1 {could not read "*": bad file number}}
        {1 {couldn't open "*": no locks available}}
        {1 {could not read "*": no locks available}}
        {1 {couldn't open "*": not a directory}}
        {1 {couldn't open "*": no such file or directory}}
        {1 {could not read "*": not a directory}}
        {1 {could not read "*": no such device}}
    }
} -cleanup {
    tsv::unset thread_ready
    thread::release $tid
    catch { cookfs::Unmount $file }
} -ok

test cookfsVfsThread-7.8 "VFS terminated while other thread gets directory listing" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression none -shared -smallfilesize 0x100000 -smallfilebuffer 0 -pagesize 0x100000]
    set dir [makeDirectory $file onedir/seconddir]
    set data1 "test"
    set file1 [makeBinFile $data1 test1 $dir]
    set tid [thread::create thread::wait]
    thread::send $tid [list set file $file]
    thread::send $tid [list set rfile $file1]
    thread::send $tid [list set rdata $data1]
    thread::send $tid [list set dir $dir]
    variable thread_done
} -body {
    thread::send -async $tid {
        # If this test works as expected, then this thread will be able to check
        # directory multiple times before the VFS is unmounted. So, here we also
        # count how many times the directory was checked successfully
        set count 0
        catch {
            while { ![tsv::exists thread_ready 2] || [incr count_ready] < 10 } {
                # We can not use just "glob -directory $dir *" here and expect
                # that this command will fail when $dir is not a directory.
                # Tcl9 doesn't throw an error and returns empty list for this
                # case. So, in order to be compatible with Tcl9, we need to
                # check whether glob's result is an empty list.
                if { ![llength [glob -nocomplain -directory $dir *]] } {
                    error "empty glob"
                }
                if { !$count } {
                    tsv::set thread_ready 1 1
                }
                incr count
            }
            set ok "notok: $count cycles"
        } result
        # Add a 0 or 1 flag to the result whether the directory was successfully
        # checked at least once.
        set result [list [expr { $count > 0 }] $result]
    } thread_done
    # wait until thread opens the file
    while { ![tsv::exists thread_ready 1] } { after 10 }
    # Unmount cookfs
    cookfs::Unmount $file
    tsv::set thread_ready 2 1
    # wait 1 threads
    vwait thread_done
    # let's retrieve the results
    thread::send $tid [list set result]
} -cleanup {
    tsv::unset thread_ready
    thread::release $tid
    catch { cookfs::Unmount $file }
} -result {1 {empty glob}}

# In this case, the file will be opened by writerchannel because it is in
# small file buffer. It will be read into the buffer from the beginning.
# Thus, all operations (read/search/close) will succeed.
test cookfsVfsThread-8.1 "Accessing a file opened for reading (small file buffer) that has been deleted from another thread" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression none -shared -smallfilesize 0x100000 -smallfilebuffer 0x100000 -pagesize 0x100000]
    set file1 [makeBinFile {TEST} test1 $file]
    set tid [thread::create thread::wait]
    thread::send $tid [list set file1 $file1]
    variable fp
} -body {
    set fp [open $file1 rb]
    thread::send $tid [list file delete $file1]
    assertEq [read $fp] "TEST"
    seek $fp 0
    close $fp
} -cleanup {
    cookfs::Unmount $file
    thread::release $tid
} -ok

test cookfsVfsThread-8.2 "Accessing a file opened for reading (from pages) that has been deleted from another thread" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression none -shared -smallfilesize 0x100000 -smallfilebuffer 0 -pagesize 0x100000]
    set file1 [makeBinFile {TEST} test1 $file]
    set tid [thread::create thread::wait]
    thread::send $tid [list set file1 $file1]
    variable fp
} -body {
    set fp [open $file1 rb]
    thread::send $tid [list file delete $file1]
    assertEq [read $fp] "TEST"
    seek $fp 0
    close $fp
} -cleanup {
    cookfs::Unmount $file
    thread::release $tid
} -ok

test cookfsVfsThread-8.3 "Accessing a file opened for writing (to small file buffer) file that has been deleted from another thread" -constraints {threaded enabledCVfs enabledTclCmds} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression none -shared -smallfilesize 0x100000 -smallfilebuffer 0x100000 -pagesize 0x100000]
    set file1 [makeBinFile {TEST} test1 $file]
    set tid [thread::create thread::wait]
    thread::send $tid [list set file1 $file1]
    variable fp
} -body {
    assertEq [[$fsid getpages] length] 0 "make sure we have no pages"
    set fp [open $file1 rb+]
    thread::send $tid [list file delete $file1]
    assertEq [read $fp] "TEST"
    puts -nonewline $fp "foo"
    seek $fp 0
    puts -nonewline $fp "bar"
    close $fp
    cookfs::Unmount $file
    # Mount the VFS again to check pages count after unmounting. The test file
    # should not be saved, so the number of pages should be the same as at
    # the beginning of the test.
    set fsid [cookfs::Mount $file $file -readonly]
    assertEq [[$fsid getpages] length] 0 "unexpected pages exist"
} -cleanup {
    cookfs::Unmount $file
    thread::release $tid
} -ok

test cookfsVfsThread-8.4 "Accessing a file opened for writing (to small file buffer) file that has been deleted from another thread" -constraints {threaded enabledCVfs enabledTclCmds} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression none -shared -smallfilesize 1 -smallfilebuffer 0 -pagesize 0x12]
    set file1 [makeFile {TEST} test1 $file]
    set tid [thread::create thread::wait]
    thread::send $tid [list set file1 $file1]
    variable fp
} -body {
    assertEq [[$fsid getpages] length] 1 "make sure we have 1 page"
    set fp [open $file1 rb+]
    thread::send $tid [list file delete $file1]
    assertEq [read $fp] "TEST\n"
    # The file has original 5 bytes + 4*0x10 bytes = 69 bytes.
    # The page size is 0x12 bytes. Thus, if the test fails, the number of pages
    # will be increased by 4 ( 69 / 0x12 )
    puts -nonewline $fp [string repeat "TEST" 0x10]
    close $fp
    cookfs::Unmount $file
    # Mount the VFS again to check pages count after unmounting. The test file
    # should not be saved, so the number of pages should be the same as at
    # the beginning of the test.
    set fsid [cookfs::Mount $file $file]
    assertEq [[$fsid getpages] length] 1 "unexpected pages exist"
} -cleanup {
    cookfs::Unmount $file
    thread::release $tid
} -ok

test cookfsVfsThread-9.1 "Test 2 threads, 1 thread reads, 2 thread writes" -constraints {threaded enabledCVfs} -setup {
    set file [makeFile {} cookfs.cfs]
    set fsid [cookfs::Mount $file $file -compression none -shared -smallfilesize 0 -smallfilebuffer 0 -pagesize 0x100]
    for { set i 0 } { $i < 20 } { incr i } {
        set fd [open [file join $file "ORIG_$i"] wb]
        puts -nonewline $fd $i[string repeat ORIG 0x100]
        close $fd
    }
    set tid1 [thread::create thread::wait]
    thread::send $tid1 [list set file $file]
    set tid2 [thread::create thread::wait]
    thread::send $tid2 [list set file $file]
    variable thread_done
} -body {
    thread::send -async $tid1 {
        set start [clock seconds]
        if { [catch {
            while { [expr { [clock seconds] - $start }] < 3 } {
                # Read all available files and check if they contain the expected data
                foreach fn_full [lsort -dictionary [glob -directory $file -type f *]] {
                    set fn [file tail $fn_full]
                    set expected [split $fn "_"]
                    set expected "[lindex $expected 1][string repeat [lindex $expected 0] 0x100]"
                    set fd [open $fn_full rb]
                    set data [read $fd]
                    close $fd
                    if { $data ne $expected } {
                        return -code error "file $fn_full contains unexpected data"
                    }
                    # puts "$fn_full - OK"
                    # The last file from the 2nd thread is THRD_100. Get out of this
                    # thread if we found it.
                    if { $fn eq "THRD_100" } {
                        return "ok"
                    }
                }
                # puts "--------NEXT ROUND----------"
            }
            set ok timeout
        } result] && $result ne "ok" } {
            set result "ERROR: $result"
        }
    } thread_done
    thread::send -async $tid2 {
        catch {
            for { set i 0 } { $i < 101 } { incr i } {
                set fd [open [file join $file "THRD_$i"] wb]
                puts -nonewline $fd $i[string repeat THRD 0x100]
                close $fd
                # puts "Created: [file join $file "THRD_$i"]"
            }
            set ok ok
        } result
    } thread_done
    # wait 2 threads
    vwait thread_done
    vwait thread_done
    cookfs::Unmount $file
    # let's retrieve the results
    assertEq [thread::send $tid1 [list set result]] "ok"
    assertEq [thread::send $tid2 [list set result]] "ok"
} -cleanup {
    thread::release $tid1
    thread::release $tid2
    catch { cookfs::Unmount $file }
} -ok

cleanupTests
