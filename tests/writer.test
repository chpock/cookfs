source [file join [file dirname [info script]] common.tcl]

test cookfsWriter-1.1 "Add small files" -setup {
    set vfs [makeFile {} pages.cfs]
    set datas [randomDatas 3 1024]
    set file1 [makeBinFile [lindex $datas 0] file1]
    set file2 [makeBinFile [lindex $datas 1] file2]
    set h [cookfs::Mount $vfs $vfs -pagesize 4096 -smallfilesize 4096 -smallfilebuffer 16384 -compression none]
    variable channel
} -body {
    set channel [open $file1 rb]
    $h writeFiles file1 channel $channel "" file2 file $file2 "" file3 data [lindex $datas 2] ""
    close $channel
    # check the files in the small file buffer
    assertEq [viewBinFile $vfs/file1] [lindex $datas 0]
    assertEq [viewBinFile $vfs/file2] [lindex $datas 1]
    assertEq [viewBinFile $vfs/file3] [lindex $datas 2]
    # flush the files to pages
    cookfs::Unmount $vfs
    set h [cookfs::Mount -readonly $vfs $vfs]
    # read the files again
    assertEq [viewBinFile $vfs/file1] [lindex $datas 0]
    assertEq [viewBinFile $vfs/file2] [lindex $datas 1]
    assertEq [viewBinFile $vfs/file3] [lindex $datas 2]
} -cleanup {
    cookfs::Unmount $vfs
    catch { close $channel }
} -ok

test cookfsWriter-1.2 "Add big files" -setup {
    set vfs [makeFile {} pages.cfs]
    set datas [randomDatas 3 1024]
    set file1 [makeBinFile [lindex $datas 0] file1]
    set file2 [makeBinFile [lindex $datas 1] file2]
    set h [cookfs::Mount $vfs $vfs -pagesize 512 -smallfilesize 512 -smallfilebuffer 16384 -compression none]
    variable channel
} -body {
    set channel [open $file1 rb]
    $h writeFiles file1 channel $channel "" file2 file $file2 "" file3 data [lindex $datas 2] ""
    close $channel
    assertEq [viewBinFile $vfs/file1] [lindex $datas 0]
    assertEq [viewBinFile $vfs/file2] [lindex $datas 1]
    assertEq [viewBinFile $vfs/file3] [lindex $datas 2]
    # flush the files to pages
    cookfs::Unmount $vfs
    set h [cookfs::Mount -readonly $vfs $vfs]
    # read the files again
    assertEq [viewBinFile $vfs/file1] [lindex $datas 0]
    assertEq [viewBinFile $vfs/file2] [lindex $datas 1]
    assertEq [viewBinFile $vfs/file3] [lindex $datas 2]
} -cleanup {
    cookfs::Unmount $vfs
    catch { close $channel }
} -ok

test cookfsWriter-2.1 "Error while adding files without parent directory" -setup {
    set vfs [makeFile {} pages.cfs]
    set h [cookfs::Mount $vfs $vfs -compression none]
} -body {
    $h writeFiles file1 data x "" somedirectory/file2 data y ""
} -cleanup {
    cookfs::Unmount $vfs
} -error {unable to add "somedirectory/file2": Unable to create entry}

test cookfsWriter-3.1 "Add the same file while previous file is in small buffer" -setup {
    set vfs [makeFile {} pages.cfs]
    set datas [randomDatas 3 1024]
    set h [cookfs::Mount $vfs $vfs -pagesize 4096 -smallfilesize 4096 -smallfilebuffer 16384 -compression none]
} -body {
    $h writeFiles file1 data [lindex $datas 0] "" file1 data [lindex $datas 1] "" file1 data [lindex $datas 2] ""
    # check the file in the small file buffer
    assertEq [viewBinFile $vfs/file1] [lindex $datas 2]
    # flush the file to pages
    cookfs::Unmount $vfs
    # the file size should be (1 data [1024 bytes] + internal data) and less than (2 datas [2048 bytes])
    assertBetween [file size $vfs] 1024 2048
    set h [cookfs::Mount -readonly $vfs $vfs]
    # read the file again
    assertEq [viewBinFile $vfs/file1] [lindex $datas 2]
} -cleanup {
    cookfs::Unmount $vfs
} -ok

test cookfsWriter-3.2 "Add the same file while previous file in in pages" -setup {
    set vfs [makeFile {} pages.cfs]
    set datas [randomDatas 3 1024]
    set h [cookfs::Mount $vfs $vfs -pagesize 1024 -smallfilesize 1024 -smallfilebuffer 1024 -compression none]
} -body {
    $h writeFiles file1 data [lindex $datas 0] "" file1 data [lindex $datas 1] "" file1 data [lindex $datas 2] ""
    # check the file
    assertEq [viewBinFile $vfs/file1] [lindex $datas 2]
    # flush the file to pages
    cookfs::Unmount $vfs
    # the file size should be (3 datas [3072 bytes] + internal data)
    assertGt [file size $vfs] { 1024 * 3 }
    set h [cookfs::Mount -readonly $vfs $vfs]
    # read the files again
    assertEq [viewBinFile $vfs/file1] [lindex $datas 2]
} -cleanup {
    cookfs::Unmount $vfs
} -ok

test cookfsWriter-4.1 "Error while adding file by absolute name" -setup {
    set vfs [makeFile {} pages.cfs]
    set h [cookfs::Mount $vfs $vfs]
} -body {
    $h writeFiles /file1 data x ""
} -cleanup {
    cookfs::Unmount $vfs
} -error {unable to add "/file1": Unable to create entry}

test cookfsWriter-5.1 "Channel options for added channels are preserved" -setup {
    set vfs [makeFile {} pages.cfs]
    set data1 [lindex [randomDatas 1 1024] 0]
    set file1 [makeBinFile $data1 file1]
    set data2 [lindex [randomDatas 1 4096] 0]
    set file2 [makeBinFile $data2 file2]
    # check small file
    set h [cookfs::Mount $vfs $vfs -pagesize 2048 -smallfilesize 2048 -smallfilebuffer 16384 -compression none]
    variable channel
} -body {

    set channel [open $file1 r]
    fconfigure $channel -encoding cp866 -translation cr
    $h writeFiles file1 channel $channel ""
    assertEq [fconfigure $channel -encoding] cp866
    assertEq [fconfigure $channel -translation] cr
    close $channel

    # try something else
    set channel [open $file1 r]
    fconfigure $channel -encoding utf-8 -translation lf
    $h writeFiles file2 channel $channel ""
    assertEq [fconfigure $channel -encoding] utf-8
    assertEq [fconfigure $channel -translation] lf
    close $channel

    # check big file

    set channel [open $file2 r]
    fconfigure $channel -encoding cp866 -translation cr
    $h writeFiles file3 channel $channel ""
    assertEq [fconfigure $channel -encoding] cp866
    assertEq [fconfigure $channel -translation] cr
    close $channel

    # try something else
    set channel [open $file2 r]
    fconfigure $channel -encoding utf-8 -translation lf
    $h writeFiles file4 channel $channel ""
    assertEq [fconfigure $channel -encoding] utf-8
    assertEq [fconfigure $channel -translation] lf
    close $channel

} -cleanup {
    cookfs::Unmount $vfs
    catch { close $channel }
} -ok

test cookfsWriter-5.2 "Channel options are preserved when error occurs" -setup {
    set vfs [makeFile {} pages.cfs]
    set data1 [lindex [randomDatas 1 1024] 0]
    set file1 [makeBinFile $data1 file1]
    set h [cookfs::Mount $vfs $vfs -pagesize 2048 -smallfilesize 2048 -smallfilebuffer 16384 -compression none]
    variable channel
} -body {

    set channel [open $file1 r]
    fconfigure $channel -encoding cp866 -translation cr
    assertErrMsg { $h writeFiles dir/file1 channel $channel "" } {unable to add "dir/file1": Unable to create entry}
    assertEq [fconfigure $channel -encoding] cp866
    assertEq [fconfigure $channel -translation] cr
    close $channel

    # try something else
    set channel [open $file1 r]
    fconfigure $channel -encoding utf-8 -translation lf
    assertErrMsg { $h writeFiles dir/file2 channel $channel "" } {unable to add "dir/file2": Unable to create entry}
    assertEq [fconfigure $channel -encoding] utf-8
    assertEq [fconfigure $channel -translation] lf
    close $channel

} -cleanup {
    cookfs::Unmount $vfs
    catch { close $channel }
} -ok

test cookfsWriter-6.1 "Deleting files in a small file buffer" -setup {
    set vfs [makeFile {} pages.cfs]
    set data1 [lindex [randomDatas 1 1024] 0]
    set data2 [lindex [randomDatas 1 4096] 0]
    set h [cookfs::Mount $vfs $vfs -pagesize 2048 -smallfilesize 2048 -smallfilebuffer 16384 -compression none]
} -body {
    $h writeFiles file1 data $data1 ""
    assertTrue [file exists $vfs/file1]
    file delete $vfs/file1
    assertFalse [file exists $vfs/file1]
    # add some real file
    $h writeFiles file2 data $data2 ""
    cookfs::Unmount $vfs
    # the file size should be (1 big data [4096 bytes] + internal data) and less than (1 big data + 1 small data [5120 bytes])
    assertBetween [file size $vfs] 4096 { 1024 + 4096 }
    set h [cookfs::Mount -readonly $vfs $vfs]
    # file1 doesn't exist
    assertFalse [file exists $vfs/file1]
    # file2 exists
    assertTrue [file exists $vfs/file2]
} -cleanup {
    cookfs::Unmount $vfs
} -ok

test cookfsWriter-6.2 "Deleting files doesn't corrupt following files in small file buffer" -setup {
    set vfs [makeFile {} pages.cfs]
    lassign [randomDatas 2 1024] data1 data2
    set h [cookfs::Mount $vfs $vfs -pagesize 4096 -smallfilesize 4096 -smallfilebuffer 16384 -compression none]
} -body {
    $h writeFiles file1 data $data1 "" file2 data $data2 ""
    assertTrue [file exists $vfs/file1]
    assertTrue [file exists $vfs/file2]
    assertEq [viewBinFile $vfs/file2] $data2
    file delete $vfs/file1
    assertFalse [file exists $vfs/file1]
    assertTrue [file exists $vfs/file2]
    assertEq [viewBinFile $vfs/file2] $data2
    cookfs::Unmount $vfs
    # the file size should be (1 data [1024 bytes] + internal data) and less than (2 datas [2048 bytes])
    assertBetween [file size $vfs] 1024 { 1024 * 2 }
    set h [cookfs::Mount -readonly $vfs $vfs]
    # file1 doesn't exist
    assertFalse [file exists $vfs/file1]
    # file2 exists
    assertTrue [file exists $vfs/file2]
    assertEq [viewBinFile $vfs/file2] $data2
} -cleanup {
    cookfs::Unmount $vfs
} -ok

cleanupTests
